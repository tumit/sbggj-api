//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.1'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'io.freefair.lombok' version '8.4'
}

group = 'dev.is-a.tumit'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'com.microsoft.sqlserver:mssql-jdbc:11.2.3.jre17'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation "org.testcontainers:junit-jupiter"
	testImplementation "org.testcontainers:mssqlserver"
}

testing {
	suites {
		configureEach {
			useJUnitJupiter()
			dependencies {
				implementation project()
				var implementations = configurations.implementation.dependencies.toList();
				var testImplementations = configurations.testImplementation.dependencies.toList();
				(implementations + testImplementations).forEach { d-> implementation(d) }
			}
		}

		integrationTest(JvmTestSuite) {
			sources {
				java {
					srcDirs = ['src/test-integration/java']
				}
				resources {
					srcDirs = ['src/test-integration/resources']
				}
			}
		}

	}
}